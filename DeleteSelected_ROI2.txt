// This macro allows the user to left mouse-click on a ROI name
// in the image what will delete the ROI from the image, ROI manager
// and results table.
// For this to work the results table has to be created using 
// the Multi Measure option (deselect "One Row Per Slice") from the
// ROI Managers menu with "Display label" selected in Set Measurements 
// (Analyze > Set Measurements).

// Used as starting point the GetCursorLocDemo macro.
// The macro stops running when the "Log" window is closed.
// Kees Straatman, University of Leicester, 12 May 2014

macro DeleteSelected_ROI2{
	if (nImages==0) exit("There is no image open");
	if (!isOpen("ROI Manager")) exit("There is no ROI manager open");
	if (!isOpen("Results")) exit("There is no results table. Create via Multi Measure option in ROI Manager");
	if (roiManager("count")==0) exit("There are no ROIs loaded in the ROI manager");
	roiManager("Associate", "true");
	roiManager("Centered", "false");
	roiManager("UseNames", "true");
	setTool("rectangle");
	roiManager("Show All with labels");
	roiManager("Show All");
	roiManager("Deselect");
	leftButton=16;
	x2=-1; y2=-1; z2=-1; flags2=-1;
	logOpened = false;

	print("Close this window when finished");
	while (!logOpened || isOpen("Log")) {
		getCursorLoc(x, y, z, flags);
		if (x!=x2 || y!=y2 || z!=z2 || flags!=flags2) { 	// Only when mouse moves new locatation is logged
		wait(20);						// Might have to be increased with large number of ROIs
             		if (flags&leftButton!=0) {
             			// Check that a ROI is selected
				if (roiManager("index")!=-1){
					RoiName= Roi.getName();
					del = false;			// Becomes "true" when a result is deleted from the table
					selectWindow("Results");
					for (i=0;i<(roiManager("count"));i++){
						if (del==false){
							// Check label ROI against labels in Results table
							S = getResultLabel(i);
							if (matches(S,".*"+RoiName+".*")){
								IJ.deleteRows(i, i);
								del = true;
							}
						}
					}
					roiManager("Delete");
				}
             			logOpened = true;
	 		}
          		x2=x; y2=y; z2=z; flags2=flags;			// Only when mouse moves new location is logged
          		wait(10);					// Takes care that one mouse click is recorded as one mouse click
      		}
	}
}